//******************************************************************************
// YAYA　ゴーストテンプレート
// 種々のイベント辞書
//******************************************************************************

//******************************************************************************
//シェル切り替え
//******************************************************************************

//------------------------------------------------------------------------------
//シェルチェンジ開始
//------------------------------------------------------------------------------

OnShellChanging
{
	'\0\s[0]着替えまーす。\w8\1着替えだけで済むのか？\e'
}

//------------------------------------------------------------------------------
//シェルチェンジ完了
//------------------------------------------------------------------------------
OnShellChanged
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\0\s[7]変身！%(_escaped_name)！\w8\s[5]\n似合うかな？\w8\1良い感じだな。\e"
}


//******************************************************************************
//インストール関連
//******************************************************************************

//------------------------------------------------------------------------------
//インストール中のメッセージ
//------------------------------------------------------------------------------
OnInstallBegin
{
	'\0\s[6]インストールしています‥\w6‥\w6\e'
}

//------------------------------------------------------------------------------
//インストール完了メッセージ
//------------------------------------------------------------------------------
OnInstallComplete
{
	_ref0 = SPLIT(reference[0],' with',2)[0]//hogehoge with hemohemo

	'\1\s[10]'
	--
	if _ref0 == 'shell' {
		'シェル'
	}
	elseif _ref0 == 'ghost' {
		'ゴースト'
	}
	elseif _ref0 == 'balloon' {
		'バルーン'
	}
	elseif _ref0 == 'plugin' {
		'プラグイン'
	}
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[1])
	"「%(_escaped_name)」、インストール完了ですよー。\n"
	--
	if basewarenameex == 'SSP' {
		_id = ''
		if _ref0 == 'shell' {
			_id = 'shell'
		}
		elseif _ref0 == 'ghost' {
			_id = 'ghost'
		}
		elseif _ref0 == 'balloon' {
			_id = 'balloon'
		}

		if _id != '' {
			'切り替えますか？'
			--
			AYATEMPLATE.MenuItem("切り替える","OnAYATEMPLATE.InstallChange",_id,200)
			--
			AYATEMPLATE.MenuItem("切り替えない","Menu_CANCEL")
		}
	}
}

OnAYATEMPLATE.InstallChange
{
	"\![change,%(AYATEMPLATE.EscapeText(reference[0])),lastinstalled]"
}

//------------------------------------------------------------------------------
//インストール失敗メッセージ
//------------------------------------------------------------------------------
OnInstallFailure
{
	_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
	"\1\s[10]インストールに失敗しましたー‥\w5‥\w5\n\n理由：%(_escaped_message)。あーあ。\e"
}

//------------------------------------------------------------------------------
//インストール拒否（他のゴーストのためのアーカイブだった）
//------------------------------------------------------------------------------
OnInstallRefuse
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\1\s[10]これは、%(_escaped_name)用のアーカイブだぞ。\w6\0\s[0]処理中断っ。\e"

}


//******************************************************************************
//消滅関連
//******************************************************************************

//------------------------------------------------------------------------------
//消滅指示
//------------------------------------------------------------------------------
OnVanishSelecting
{
	'\0\s[4]そ、そんなぁ\w5‥\w5‥\1\s[10]あきらめるのはまだ早いぞ。\e'
}

//------------------------------------------------------------------------------
//消滅開始
//------------------------------------------------------------------------------
OnVanishSelected
{
	'\0\s[4]‥\w5‥\w5さようなら。\w8\1また会えることを楽しみにしてるぞ。\w8\0\n\n‥\w5‥\w5化けて出てやる。\w8\1\s[11]\n\nちょっ！\e'
}

//------------------------------------------------------------------------------
//消滅中止
//------------------------------------------------------------------------------
OnVanishCancel
{
	"\0\s[4]ぷー。\w8\1助かったな‥\w5‥\w5\w8\0\s[7]\n\n%(username)！冗談にもほどがあるよ！\w8\1\n\n果たして冗談だったのかな？\w8\0\s[4]\n\n‥\w5‥\w5そう信じたいよ。\e"
}

//------------------------------------------------------------------------------
//他のゴーストが消滅して自分に切り替わった
//------------------------------------------------------------------------------
OnVanished
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	'\1\s[10]命ある物はいつか滅び、命無きゴーストもいつかは電子の海に消えゆく運命。\w8\0\s[0]\w9…\w9…\w9\e'
}



//******************************************************************************
//ネットワーク更新
//******************************************************************************

//------------------------------------------------------------------------------
//ネットワーク更新開始
//------------------------------------------------------------------------------
OnUpdateBegin
{
	'\1\s[10]OnUpdateBegin\w9......'
}

//------------------------------------------------------------------------------
//ネットワーク更新準備完了
//------------------------------------------------------------------------------
OnUpdateReady
{
	"\1\s[10]%(TOINT(reference[0])+1)個のファイルをダウンロードします。\w9\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新終了
//------------------------------------------------------------------------------
OnUpdateComplete
{
	if reference[0] == 'none'{
		//---- 更新すべきファイルが無かった
		'\1\s[10]更新すべきファイルがなかったよ'
	}
	else{
		//---- 実際に更新が完了した
		'\0\s[0]更新完了\e'
	}
}

//------------------------------------------------------------------------------
//ネットワーク更新失敗
//------------------------------------------------------------------------------
OnUpdateFailure
{
	if reference[0] == 'timeout'{
		//---- タイムアウトした
		'\1\s[10]タイムアウト。'
	}
	elseif reference[0] == 'md5 miss'{
		//---- MD5値が一致しなかった
		'\1\s[10]md5 miss'
	}
	else{
		//---- 上記以外の理由で失敗した
		_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
		"\1\s[10]%(_escaped_message)、だって。\w5\0\s[0]うーん。"
	}
	--
	'\n\n\1というわけで、ネットワーク更新、失敗だね。\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新 ダウンロード開始
//------------------------------------------------------------------------------
OnUpdate.OnDownloadBegin
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\1\s[10]ファイル「%(_escaped_name)」をダウンロードします。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値照合開始
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareBegin
{
	'\1\s[10]ファイルの確認ー。\w8\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	
	'\1\s[10]いいみたいだね。\0\s[0]うむ。\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値不一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	'\1\s[10]OnMD5CompareFailure'
}



//******************************************************************************
//ファイル作成関連
//******************************************************************************

//------------------------------------------------------------------------------
//updates2.dau作成開始
//------------------------------------------------------------------------------
OnUpdatedataCreating
{
	'\1\s[10]アップデートデータを作成しています。\e'
}

//------------------------------------------------------------------------------
//updates2.dau作成完了
//------------------------------------------------------------------------------
OnUpdatedataCreated
{
	'\1\s[10]アップデートデータ、ちゃんと作れたよ。\w8\0よしよし。\e'
}

//------------------------------------------------------------------------------
//NAR作成開始
//------------------------------------------------------------------------------
OnNarCreating
{
	'\1\s[10]narファイルを作成するよ。\e'
}

//------------------------------------------------------------------------------
//NAR作成完了
//------------------------------------------------------------------------------
OnNarCreated
{
	"\1\s[10]「%(SHIORI3FW.EscapeAllTags(reference[0]))」のnarファイル、「%(SHIORI3FW.EscapeAllTags(reference[1]))」が爽やかに完成！\w8\0爽やかにって。\e"
}

//******************************************************************************
//時計合わせ
//みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏
//******************************************************************************

//------------------------------------------------------------------------------
//時計合わせ開始
//------------------------------------------------------------------------------
OnSNTPBegin
{
	"\1\s[10]時計、合わせまーす。\w8\0%(SHIORI3FW.EscapeAllTags(reference[0]))に接続するぞ。\e"
}

//------------------------------------------------------------------------------
//時刻取得成功、比較開始
//この関数で 2008/03/25 19:15:05 のように見やすい形に作り変えます。
//詳しくはSTRFORM関数の説明を見てね！
//わからなければこのままで。
//------------------------------------------------------------------------------

SNTPCompare_StrForm
{
	_txt = SPLIT(_argv[0],',')
	STRFORM('$04d/$02d/$02d $02d:$02d:$02d',TOINT(_txt[0]),TOINT(_txt[1]),TOINT(_txt[2]),TOINT(_txt[3]),TOINT(_txt[4]),TOINT(_txt[5]))
}

OnSNTPCompare
{
	"\0\s[0]\_q/
	修正前 %(SNTPCompare_StrForm(reference[2]))\n/
	修正後 %(SNTPCompare_StrForm(reference[1]))\n/
	\_q\n"
	--
	if reference[3] == 0{
		'うーん、ぴったり。\e'
	}
	else{
		//\6は補正実行タグ。消しちゃだめ
		"\6\0\n\s[0]%(SHIORI3FW.EscapeAllTags(reference[3]))秒のずれを修正したよ。"
	}
}

//------------------------------------------------------------------------------
//時計合わせ失敗
//------------------------------------------------------------------------------
OnSNTPFailure
{
	'\0\s[0]‥\w5‥\w5時計合わせ失敗。\w8\1なにしてんだよ。\e'
}


//******************************************************************************
//そのほか
//******************************************************************************

//------------------------------------------------------------------------------
//ディスプレイ解像度変更
//------------------------------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference[0]
	displaywidth  = reference[1]
	displayheight = reference[2]
	SHIORI3FW.RegisterTempVar('displaybpp', 'displaywidth', 'displayheight')
}

//------------------------------------------------------------------------------
//URLドロップ
//CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます
//------------------------------------------------------------------------------
OnURLDropping
{
	"\0\s[0]%(SHIORI3FW.EscapeAllTags(reference[0]))からデータを取得してるよ。\e"
}

//------------------------------------------------------------------------------
//起動時に MATERIA を検出
//------------------------------------------------------------------------------
OnEmbryoExist
{
	'\1\s[10]あれ、「伺か」が起動してるよ。\w8\0ありがたやありがたや。\w8\1\s[10]\n\nなんで拝むの？\e'
}

//------------------------------------------------------------------------------
//起動時に猫どりふを検出
//------------------------------------------------------------------------------
OnNekodorifExist
{
	'\0\s[0]うわ、猫どりふだ‥\w5‥\w5\w8\nまだあったのか！\e'
}

//------------------------------------------------------------------------------
//スクリーンセーバー実行開始
//------------------------------------------------------------------------------
OnScreenSaverStart
{
	'\0\s[0]画面の焼き付きを保護する開始っ！\w8\e'
}

//------------------------------------------------------------------------------
//スクリーンセーバー実行終了
//------------------------------------------------------------------------------
OnScreenSaverEnd
{
	'\1\s[10]‥\w5‥起きろ。\w9\nスクリーンセーバーが無くなったぞ。\w8\0\s[0]わ、いつの間に‥\w5‥\w5\e'
}
